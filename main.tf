locals {
  smart_detector_alert_rules = {
    "dependency_latency_degradation" = {
      name          = "Dependency Latency Degradation"
      detector_type = "DependencyPerformanceDegradationDetector"
      description   = "Dependency Latency Degradation notifies you of an unusual increase in response by a dependency your app is calling (e.g. REST API or database)"
      severity      = "Sev3"
      frequency     = "P1D"
    }

    "exception_anomalies" = {
      name          = "Exception Anomalies"
      detector_type = "ExceptionVolumeChangedDetector"
      description   = "Exception Anomalies notifies you of an unusual rise in the rate of exceptions thrown by your app."
      severity      = "Sev3"
      frequency     = "P1D"
    }

    "failure_anomalies" = {
      name          = "Failure Anomalies"
      detector_type = "FailureAnomaliesDetector"
      description   = "Failure Anomalies notifies you of an unusual rise in the rate of failed HTTP requests or dependency calls."
      severity      = "Sev3"
      frequency     = "PT1M"
    }

    "potential_memory_leak" = {
      name          = "Potential Memory Leak"
      detector_type = "MemoryLeakDetector"
      description   = "Potential Memory Leak notifies you of increased memory consumption pattern by your app which may indicate a potential memory leak."
      severity      = "Sev3"
      frequency     = "P1D"
    }

    "response_latency_degradation" = {
      name          = "Response Latency Degradation"
      detector_type = "RequestPerformanceDegradationDetector"
      description   = "Response Latency Degradation notifies you of an unusual increase in latency in your app response to requests."
      severity      = "Sev3"
      frequency     = "P1D"
    }

    "trace_severity_degradation" = {
      name          = "Trace Severity Degradation"
      detector_type = "TraceSeverityDetector"
      description   = "Trace Severity Degradation notifies you of an unusual increase in the severity of the traces generated by your app."
      severity      = "Sev3"
      frequency     = "P1D"
    }
  }
}

resource "azurerm_application_insights" "this" {
  name                = var.component_name
  resource_group_name = var.resource_group_name
  location            = var.location
  application_type    = "web"
  workspace_id        = var.workspace_id

  # Irrelevant when "workspace_id" is set.
  # Retention must be configured in Log Analytics workspace.
  retention_in_days = null

  tags = var.tags
}

resource "azurerm_monitor_smart_detector_alert_rule" "this" {
  for_each = local.smart_detector_alert_rules

  # Consecutive "replace" function calls does not scale well.
  # Possible options are:
  # - "template_file" data source: superseded by "templatefile" function.
  # - "templatefile" function: only supports template file, not template string.
  name                = replace(replace(var.smart_detector_alert_rule_name_template, "$${ smart_detector_alert_rule_name }", each.value.name), "$${component_name}", var.component_name)
  resource_group_name = var.resource_group_name
  scope_resource_ids  = [azurerm_application_insights.this.id]
  enabled             = true

  detector_type = each.value["detector_type"]
  description   = each.value["description"]
  severity      = each.value["severity"]
  frequency     = each.value["frequency"]

  action_group {
    ids = [var.action_group_id]
  }

  tags = var.tags
}

# Ref: https://learn.microsoft.com/nb-no/azure/azure-monitor/alerts/alerts-smart-detections-migration#migrate-your-smart-detection-by-using-arm-templates
resource "azapi_update_resource" "this" {
  type      = "Microsoft.Insights/components/ProactiveDetectionConfigs@2018-05-01-preview"
  parent_id = azurerm_application_insights.this.id
  name      = "migrationToAlertRulesCompleted"

  body = jsonencode({
    properties = {
      SendEmailsToSubscriptionOwners = false
      CustomEmails                   = []
      Enabled                        = true
    }
  })

  depends_on = [azurerm_monitor_smart_detector_alert_rule.this]
}
